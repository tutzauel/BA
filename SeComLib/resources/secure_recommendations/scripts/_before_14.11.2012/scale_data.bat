@echo off

REM Scales the training and test data according to the scales generated by svm-scale.exe

REM Set directories
set DATA_DIR=D:\mtodor\Desktop\training_test\data\
set MATLAB_DIR=%DATA_DIR%MATLAB\
set TEST_DATA_DIR=%DATA_DIR%test\
set MEDICAL_RELEVANCE_DIR=%DATA_DIR%training\medical_relevance\
set SAFETY_DIR=%DATA_DIR%training\safety\

REM Set files
set SCALES_FILE=scales.out
set COMPUTE_SCALES_FILE=philips.scaleme

REM Compute scales from philips.scaleme
svm-scale -s %MATLAB_DIR%%SCALES_FILE% %MATLAB_DIR%%COMPUTE_SCALES_FILE%

REM Create test data directory
if not exist %TEST_DATA_DIR% (
	echo Creating directory %TEST_DATA_DIR%
	mkdir %TEST_DATA_DIR%
)
REM Create medical relevance training data directory
if not exist %MEDICAL_RELEVANCE_DIR% (
	echo Creating directory %MEDICAL_RELEVANCE_DIR%
	mkdir %MEDICAL_RELEVANCE_DIR%
)
REM Create safety training data directory
if not exist %SAFETY_DIR% (
	echo Creating directory %SAFETY_DIR%
	mkdir %SAFETY_DIR%
)

REM Apply scales to the test data
for %%f in (%MATLAB_DIR%philips.test*) do (
	echo Creating %TEST_DATA_DIR%%%~nxf & echo.
	svm-scale -r %MATLAB_DIR%%SCALES_FILE% %%f > %TEST_DATA_DIR%%%~nxf
)

REM Apply scales to the medical relevance training data
for %%f in (%MATLAB_DIR%cluster*.train) do (
	echo Creating %MEDICAL_RELEVANCE_DIR%%%~nxf & echo.
	svm-scale -r %MATLAB_DIR%%SCALES_FILE% %%f > %MEDICAL_RELEVANCE_DIR%%%~nxf
)

REM Apply scales to the safety training data
for %%f in (%MATLAB_DIR%qolunsafe*.train) do (
	echo Creating %SAFETY_DIR%%%~nxf & echo.
	svm-scale -r %MATLAB_DIR%%SCALES_FILE% %%f > %SAFETY_DIR%%%~nxf
)